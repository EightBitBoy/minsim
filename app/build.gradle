buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'io.realm:realm-gradle-plugin:3.3.2'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.2.2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        classpath 'com.atlassian.commonmark:commonmark:0.9.0'
    }
}

import org.commonmark.node.Node;
import org.commonmark.parser.Parser;
import org.commonmark.renderer.html.HtmlRenderer;


apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'spoon'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.0'
    defaultConfig {
        applicationId 'de.eightbitboy.minsim'
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "0.1.0"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            manifestPlaceholders = [
                    appName: "Minsim Debug"]
            debuggable = true

            applicationIdSuffix ".debug"
        }
        release {
            manifestPlaceholders = [
                    appName: "Minsim"]
            debuggable = false

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
}


spoon {
    debug = true
    sequential = false
    grantAllPermissions = true
}

task processMarkdown {
    File assets = new File('app/src/main/assets')
    if( !assets.exists() ) {
        assets.mkdirs()
    }

    Parser parser = Parser.builder().build()
    HtmlRenderer renderer = HtmlRenderer.builder().build()

    new File('app/src/main/assets/about.html').withWriter('UTF-8') { w ->
        w.write(renderer.render(parser.parse(new File("README.md").text)))
    }

    new File('app/src/main/assets/changelog.html').withWriter('UTF-8') { w ->
        w.write(renderer.render(parser.parse(new File("CHANGELOG.md").text)))
    }
}

preBuild {
    dependsOn processMarkdown
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:26.0.0-alpha1'
    compile 'com.android.support:design:26.0.0-alpha1'
    compile 'com.android.support:recyclerview-v7:26.0.0-alpha1'
    compile 'com.android.support:support-v4:26.0.0-alpha1'
    compile 'com.android.support:support-vector-drawable:26.0.0-alpha1'
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'com.jakewharton:butterknife:8.6.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'

    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.3.2'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile 'com.squareup.spoon:spoon-client:1.7.1'
}
